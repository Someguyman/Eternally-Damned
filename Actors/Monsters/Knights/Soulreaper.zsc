
Class MM_SoulReaper : MM_HellKnight 
{
  Default
  {
    Health 400;
    Radius 24;
    Height 64;
    Speed 7;
	scale 1.05;
    PainChance 35;
	mass 750;
    SeeSound "harvester/sight";
    PainSound "hellknight/pain";
    DeathSound "harvester/death";
    ActiveSound "hellknight/active";
    Obituary "%o couldn't evade the soul Reaper's attack.";
	Tag "Soul Reaper";
    MONSTER;
    +FLOORCLIP
  }
	
	Void A_Revwalk()
	{
		A_Chase();
		A_StartSound("skeleton/walk", CHAN_BODY, CHANF_DEFAULT, 1, ATTN_IDLE);
	}
	
  States
  {
  Spawn:
    SLRP AB 10 A_Look();
    Loop;
  See:
	SLRP A 3 A_Revwalk();
	SLRP ABB 3 A_Chase;
	SLRP C 3 A_Revwalk();
	SLRP CDD 3 A_Chase;
	Loop;
  Melee:
	SLRP E 4 A_FaceTarget();
	SLRP F 5 A_FaceTarget();
	SLRP G 6 A_CustomMeleeAttack(random(1, 10) * 6, "Imp/melee");
	Goto see;
  Missile:
    SLRP KLM 6 Bright A_FaceTarget();
    SLRP N 8 Bright 
	    {
			A_FaceTarget();
			A_SpawnProjectile("MM_SoulHarvesterBall", 45, 0, -45);
			A_SpawnProjectile("MM_SoulHarvesterBall", 45, 0,   0);
			A_SpawnProjectile("MM_SoulHarvesterBall", 45, 0,  45);
		}	
    SLRP M 6;
	SLRP O 6;
    Goto See;
  Pain:
    SLRP P 3;
    SLRP P 3 A_Pain();
    Goto See;
  Death:
	SLRP Q 1;
    SLRP Q 6 A_Scream();
    SLRP RST 7;
    SLRP U 7 A_SpawnItemEx("MM_SoulHarvesterGhost", 0,0,32, 0,0,0, 0, SXF_CLIENTSIDE);
    SLRP V 6;
    SLRP W 6 A_Fall();
	SLRP XY 6;
    SLRP Z -1;
    Stop;
  Death.SSGGIBING:
	Stop;
  XDeath:
    Stop;
  Raise:
    SLRP WVUTSRQPO 5;
    Goto See;
  }
}
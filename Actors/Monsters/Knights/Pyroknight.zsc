
class MM_PyroKnight : MM_HellKnight
{
	Default
	{
		Speed 10;
		Scale 1.05;
		+NOFEAR
		SeeSound "InfernoBaron/Sight";
		ActiveSound "InfernoBaron/Active";
		PainSound "InfernoBaron/Pain";
		DeathSound "InfernoBaron/Death";
		BloodColor "ed 4c 0c";
		Obituary "%o was burned to cinders by a Pyro Knight.";
		HitObituary "%o got ripped to shreds by a Pyro Knight.";
		BloodType "MM_Brightblood";
		Tag "Pyro knight";
	}
	
	Void A_Pyroattack()
	{
	
		A_CustomComboAttack("MM_Pyrofireball", 28, 40, "baron/melee");
		A_Facetarget();
	
	}
	
	Void A_Pyroattack2()
	{
	
		A_CustomComboAttack("MM_PyroShot", 28, 40, "baron/melee");
		A_Facetarget();
	
	}
	
	Void A_Pyrowalk()
	{
	
		if (CallACS("Projtoggle")==1)
		{
			A_SpawnItemEx("MM_WalkFire",0,0,0,0,0,0,0,128);
		}
		A_Chase();
	
	}
	
	Void A_Pyrowalk2()	// Same as Pyro walk, but with the noble hoof sound
	{
		if (CallACS("Projtoggle")==1)
		{
			A_SpawnItemEx("MM_WalkFire",0,0,0,0,0,0,0,128);
		}
		A_StartSound("Baronofhell/Walk", CHAN_BODY, CHANF_DEFAULT, 1, ATTN_IDLE);
		A_Chase();
	
	}
	
	Void A_Pyrosoul()
	{
		if (CallACS("Projtoggle")==1)
		{
			A_SpawnProjectile("MM_PyroSoul",86,0,0,2,-90);
		}
	}
	
	States
	{
	Spawn:
		PYRO AB 10 Bright A_Look;
		Loop;
	See:
		PYRO A 3 Bright A_Pyrowalk2();
		PYRO ABB 3 Bright A_Pyrowalk();
		PYRO C 3 Bright A_Pyrowalk2();
		PYRO CDD 3 Bright A_Pyrowalk();
		Loop;
	Missile:
		PYRO A 0 Bright A_JUMP(70,"Missile2");
	Melee:
		PYRO E 4 Bright A_FaceTarget;
		PYRO F 4 Bright A_Pyroattack();
		PYRO G 4 Bright A_Pyroattack();
		PYRO G 4 Bright A_Pyroattack2();
		PYRO H 4 Bright A_FaceTarget;
		PYRO I 4 Bright A_Pyroattack();
		PYRO J 4 Bright A_Pyroattack();
		PYRO J 4 Bright A_Pyroattack2();
		Goto See;
	Missile2:
		PYRO K 8 Bright A_FaceTarget;
		PYRO L 8 Bright A_FaceTarget;
		PYRO MMMM 4 Bright A_Pyroattack2();
		Goto See;
	Pain:
		PYRO N 2 Bright;
		PYRO N 2 Bright A_Pain;
		Goto See;
	Death.Ice:
		PYRO H 5 A_GenericFreezeDeath;
		TNT1 A 1 A_Freezedeathchunks;
		WAIT;
	Death.SSGGIBING:
		TNT1 A 0 A_Jump(60, "Death");
		TNT1 A 0 A_Jumpifcloser(128, "XDeath");
		TNT1 A 0 A_Jump(256, "Death");
		Goto Death;
	Death:
		PYRO O 1 Bright;
		PYRO O 9 Bright A_Scream();
		PYRO PQ 10;
		PYRO R 10 A_Fall(); 
		PYRO S 6 { A_Pyrosoul(); A_StartSound("InfernoBaron/Fade", CHAN_AUTO, CHANF_DEFAULT, 1, ATTN_IDLE); }
		PYRO TUVWXY 6;
		PYRO Z -1;
		Stop;
	XDeath:
		PYDX A 6 Bright;
		PYDX B 6 Bright A_StartSound("InfernoBaron/Death2", CHAN_VOICE, CHANF_DEFAULT, 1, ATTN_IDLE);
		PYDX C 6 Bright;
		PYDX D 6 Bright { A_NoBlocking(); A_StartSound("InfernoBaron/XDeath", CHAN_VOICE, CHANF_DEFAULT, 1, ATTN_IDLE); }
		PYDX EF 6 Bright;
		PYDX G 6 Bright A_Pyrosoul();
		PYDX H 6 Bright;
		PYDX I 6 Bright A_StartSound("InfernoBaron/Fade", CHAN_AUTO, CHANF_DEFAULT, 1, ATTN_IDLE);
		PYDX JKL 6;
		PYDX M -1;
		Stop;
	Raise:
		PYRO Y 5;
		PYRO XWVUTSRQ 5;
		PYRO PO 5 Bright;
		Goto See;
	Crush:
		PYRO Z 0;
		PYRO Z -1;
		stop;
	}
}

class MM_PyroShot : MancubusShot
{
	Default
	{
		Renderstyle "Add";
		+ZDOOMTRANS
		SeeSound "Firebig/Spawn";
		DeathSound "Firebig/Death";
		SPEED 30; 
		FastSpeed 40;
		DamageFunction 31; 
	}
	States
	{
	Spawn:
		PBL2 A 0 nodelay A_JumpIf(CallACS("Projtoggle")==1, "AltSpawn");
		PBL2 AB 4 BRIGHT;
		PBL2 A 0 A_JumpIf(CallACS("Projtoggle")==1, "AltSpawn");
		Loop;
  Altspawn:
		PYBL AAA 1 Bright A_SpawnItemEx("MM_PyroTrail", 0, 0, 0, 0, 0, 0, 0, 128);
		PYBL A 1 Bright
		{
			A_SpawnItemEx("MM_PyroTrail", 0, 0, 0, 0, 0, 0, 0, 128);
			A_SpawnItemEx("MancubusTrail", 0, 0, 2, 0, 0, 0, 0, 128);
		}
		PYBL BBB 1 Bright A_SpawnItemEx("MM_PyroTrail", 0, 0, 0, 0, 0, 0, 0, 128);
		PYBL B 1 Bright
		{
			A_SpawnItemEx("MM_PyroTrail", 0, 0, 0, 0, 0, 0, 0, 128);
			A_SpawnItemEx("MancubusTrail", 0, 0, 2, 0, 0, 0, 0, 128);
		}
		Goto spawn;
	 Death:
		MNFX ABC 6 Bright;
		Stop;
	}
}

class MM_Pyrofireball : MM_Fastimpball
{
	Default
	{
		SeeSound "Superimp/attack";
	}
	
	Void A_Trail()
	{
		A_SpawnItemEx("MM_Fasttrail2", 0, 0, 0, 0, 0, 0, 0, 128);
		A_SpawnItemEx("Ball1Trail", 0, 0, 0, 0, 0, 0, 0, 128);
	}
	
	States
	{
	Spawn:
		BRB2 A 0 nodelay A_JumpIf(CallACS("Projtoggle")==1, "AltSpawn");
		BRB2 AB 4 BRIGHT;
		BRB2 A 0 A_JumpIf(CallACS("Projtoggle")==1, "AltSpawn");
		Loop;
	Altspawn:
		BL1M AA 1 BRIGHT A_SpawnItemEx("MM_Fasttrail2", 0, 0, 0, 0, 0, 0, 0, 128);
		BL1M A 1 BRIGHT A_Trail();
		BL1M BB 1 BRIGHT A_SpawnItemEx("MM_Fasttrail2", 0, 0, 0, 0, 0, 0, 0, 128);
		BL1M B 1 BRIGHT A_Trail();
		Goto spawn;
	Death:
		BRB2 CDEFGHI 3 Bright;
		Stop;
	}
}

Class MM_Fasttrail2 : MM_Fasttrail
{
  Default
  {
	Scale 1.0;
	+ZDOOMTRANS
  }
}

Class MM_PyroTrail : Actor
{
  Default
  {
    Radius 1;
    Height 1;
    Speed 0;
	+ZDOOMTRANS
    RenderStyle "Add";
    Alpha 0.70;
    Projectile;
    +NoClip
  }

  States
  {
  Spawn:
	tnt1 A 2;
    PYBL CDEFGHIJ 2 Bright;
    Stop;
  }
}

Class MM_WalkFire : Actor
{
	Default
	{
		+NOBLOCKMAP
		+NOCLIP;
		+DROPOFF;
		+RANDOMIZE;
		+NOINTERACTION;
		+NOTELEPORT
		Damage 0;
		//+MISSILE
		RenderStyle "Add";
		Alpha 0.67;
  }
  States
  {
  Spawn:
    TNT1 A 8;
    FRTF ABC 3 Bright;
    FRTF AAA 0 A_SpawnItemEx("MM_WalkFire",0,0,0,0,0,0,random(0,360),0,220);
    FRTF DEFGHIJKLMNO 3 Bright;
    Stop;
  }
}

Class MM_PyroSoul : Actor
{
  Default
  {
    Radius 1;
    Height 1;
    Speed 3;
    Projectile;
	RenderStyle "Add";
	Alpha 0.90;
	SeeSound "InfernoBaron/Soul";
    +ClientSideOnly
  }

  States
  {
  Spawn:
    PYSL AB 6 Bright;
	PYSL C 6 Bright A_SetRenderStyle(0.70, STYLE_Add);
	PYSL D 6 Bright;
    Stop;
  }
}
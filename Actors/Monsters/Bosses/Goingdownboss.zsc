
class MM_GoingDown_Boss : MM_Spidermastermind
{
	Default
	{
		Health 12000;
		Speed 16;
		Radius 64;
		Height 100;
		Mass 2000;
		PainChance 40;
		+DOHARMSPECIES
		+NOFEAR
		Scale 1.5;
		Monster;
		SeeSound "";
		PainSound "brain/pain";
		DeathSound "brain/death";
		Tag "Enraged Mastermind";
		Obituary "%o stood in awe of an Angry Spider Mastermind.";
	}
	
		Void A_Spiderattack2()
	{
		A_Startsound("Weapons/NailMGFire");
		A_FaceTarget();
		A_Spawnprojectile("MancubusShot",70,0,0,CMF_OFFSETPITCH,frandom(-0.5,0.5));
		A_Spawnprojectile("MancubusShot",70,0,-10,CMF_OFFSETPITCH,frandom(-0.5,0.5));
		A_Spawnprojectile("MancubusShot",70,0,10,CMF_OFFSETPITCH,frandom(-0.5,0.5));
	}
	
		Void A_SingleRevAttack()
	{
		A_Facetarget();
		if (CallACS("Projtoggle")==1)
		{
			A_SpawnProjectile("MM_RevenantTracer",70,10);
		}
		else
		{
			A_SpawnProjectile("MM_OldRevenantTracer",70,10);
		}
	}
	
		Void A_Spiderexplode()
	{
		if (CallACS("Projtoggle")==1)
		{
			A_SpawnProjectile("BossExplosion5",FRandom(20,80),FRandom(20,-20)*2,FRandom(0,360)*2,2,FRandom(0,360)*2);
		}
		else
		{
			
		}
	}
	
	Void A_Spiderattack3()
	{
		A_Startsound("Weapons/GrenadeFire");
		A_FaceTarget();
		A_Spawnprojectile("MM_Mastermind_Grenade",70,0,0);
	}
	
	States
	{
	Spawn:
		GDBO AB 10 A_Look;
		Loop;
	See:
		GDBO A 3 A_Mastermindwalk();
		GDBO ABB 3 A_Chase;
		GDBO C 3 A_Mastermindwalk2();
		GDBO C 3 A_Chase;
		GDBO D 3 A_Mastermindwalk();
		GDBO DEE 3 A_Chase;
		GDBO F 3 A_Mastermindwalk2();
		GDBO F 3 A_Chase;
		Loop;
	Missile:
		GDBO A 0 A_JumpIfCloser(300,"Missile3");
		GDBO A 21 A_FaceTarget;
		GDBO A 0 A_JumpIfCloser(700,"Missile2");
		GDBO G 2 BRIGHT;
		GDBO H 2 BRIGHT A_Spiderattack2();
		GDBO T 1 BRIGHT;
		GDBO H 0 BRIGHT A_SpidRefire;
		Goto Missile+2;
	Missile2:
		GDBO G 2 BRIGHT;
		GDBO H 2 BRIGHT A_Spiderattack1();
		GDBO H 0 BRIGHT A_SpidRefire;
		Goto Missile+2;
	Missile3:
		GDBO T 8 A_FaceTarget;
		GDBO H 6 Bright A_Spiderattack3();
		GDBO U 8 A_FaceTarget;
		GDBO T 8 A_FaceTarget;
		GDBO H 6 Bright A_Spiderattack3();
		GDBO U 8 A_FaceTarget;
		GDBO T 13 A_SpidRefire;
		Goto Missile+2;
	Pain:
		GDBO I 3;
		GDBO I 3 A_BrainPain;
	Painattack:
		GDBO T 10 Bright A_FaceTarget;
		GDBO H 4 Bright A_FatAttack1();
		GDBO G 4 Bright A_FaceTarget;
		GDBO H 4 Bright A_FatAttack2();
		GDBO G 4 Bright A_FaceTarget;
		GDBO H 4 Bright A_SingleRevAttack();
		GDBO G 4 Bright A_FaceTarget;
		GDBO H 4 Bright A_SingleRevAttack();
		Goto See;
	Death:
		GDBO I 1 Bright;
		GDBO J 19 Bright A_Scream;
		GDBO J 0 Bright A_NoBlocking;
		GDBO KL 10 Bright A_Spiderexplode();
		GDBO M 8 Bright A_Spiderexplode();
		GDBO M 2 Bright A_Spiderexplode();
		GDBO NNOOPPQQ 5 Bright A_Spiderexplode();
		GDBO R 10 Bright;
		GDBO S 30;
		GDBO S -1 A_BrainDie();
		Stop;
	Crush:
		GDBG A 0;
		GDBG A 0 A_Startsound("Bloody/crush");
		GDBG A -1;
		stop;
	}
}

Class BossExplosion5 : BossExplosion
{
	Default
	{
	scale 1.5;
	}
	states
	{
	Spawn:
		EPO2 B 0 bright;
		EPO2 B 6 bright A_Startsound("Epic/Expolsions", CHAN_BODY);
		EPO2 C 4 bright A_FadeOut (0.12);
		EPO2 D 3 bright A_FadeOut (0.12);
		Stop;
	}
}
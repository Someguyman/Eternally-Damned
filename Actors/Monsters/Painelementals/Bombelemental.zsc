
class MM_BombElemental : MM_PainElemental
{
	Default
	{
		Health 800;
		Radius 31;
		Height 56;
		Mass 400;
		Speed 12;
		PainChance 100;
		Monster;
		Scale 1.2;
		+ZDOOMTRANS
		+FLOAT +NOGRAVITY +NOINFIGHTSPECIES +DONTHARMSPECIES +NORADIUSDMG +MISSILEEVENMORE +NOFEAR +DONTGIB +NEVERRESPAWN
		SeeSound "Bombcaco/sight";
		PainSound "Bombcaco/pain";
		DeathSound "Bombcaco/explode";
		ActiveSound "Bombcaco/active";
		Species "Cacodemon";
		BloodColor "Red";
		Obituary "\cg%o was blown up by an Anger Elemental";
		Tag "Bomb elemental";
	}
	States
	{
	Spawn:
		INFR ABCDCBA 4 Bright A_Look;
		Loop;
	See:
		INFR ABCDCBA 4 Bright A_Chase;
		Loop;
	Melee:
		INFR HI 5 Bright A_FaceTarget;
		INFR JJJJJJJ 2 Bright A_SpawnProjectile("FireTrail", 22, 10);
		INFR IH 5 Bright A_FaceTarget;
		Goto see;
	Missile:
		INFR A 0 A_JumpIfCloser(260,"melee");
		INFR H 5 Bright A_FaceTarget;
		INFR I 5 Bright A_FaceTarget;
		INFR J 5 BRIGHT A_CustomComboAttack("Lavaball", 32, 10 * random(1, 10), "caco/melee");
		Goto See;
	Pain:
		INFR A 0 A_JumpIfHealthLower(250,"Kamikasi");
		INFR K 6 Bright A_Pain;
		INFR K 6 Bright;
		Goto See;
	Death:
		INFR L 1 BRIGHT {bFLOATBOB = False;}
		INFR L 7 Bright A_Scream;
		INFR M 8 Bright;
		INFR N 8 Bright A_Explode(256, 160, 1);
		INFR O 8 Bright
		{
			A_SpawnProjectile("Lavaball",32,0, 45, CMF_AIMDIRECTION);
			A_SpawnProjectile("Lavaball",32,0,135, CMF_AIMDIRECTION);
			A_SpawnProjectile("Lavaball",32,0,225, CMF_AIMDIRECTION);
			A_SpawnProjectile("Lavaball",32,0,315, CMF_AIMDIRECTION);
		}
		INFR P 8;
		INFR Q 5;
		Stop;
	Kamikasi:
		TNT1 A 0 A_JumpIfCloser(1250,1);
		Goto Pain+1;
		TNT1 A 0 A_Jump(18,"See");
		INFR E 0 Bright {bNOPAIN = true;}
		INFR E 0 Bright {bNOTARGET = true;}
		INFR E 0 Bright A_Startsound("Bombcaco/anger");
		INFR EFG 3 Bright A_FaceTarget;
		INFR G 0 Bright A_SpawnProjectile("BombElementalKamakasi",0,0,0,0,0);
		TNT1 A 0 A_Die("Suicide");
		Stop;
	Death.Suicide:
		TNT1 A 100 A_NoBlocking;
		Stop;
	}
}

class BombElementalKamakasi : Actor
{
	Default
	{
		Health 100;
		Radius 31;
		Height 56;
		Speed 25;
		damage 50;
		PROJECTILE;
		Scale 1.2;
		SeeSound "";
		DeathSound "Bombcaco/explode";
		BounceFactor 1.0;
		BounceType "Classic";
		+SOLID
		+SHOOTABLE
		-NOBLOCKMAP
		+NOTARGETSWITCH
		+SEEKERMISSILE
		+THRUSPECIES
	}
	States
	{
	Spawn:
		INFR I 2 Bright A_SeekerMissile(10,0);
		INFR I 0 Bright A_SpawnItemEx("FireTrailparticle",-10,0,20,0,0,0,0,128);
		INFR J 2 Bright A_SeekerMissile(10,0);
		Loop;
	Death:
		INFR L 5 Bright  A_Explode(256, 160, 1);
		INFR M 5 Bright;
		INFR N 5 Bright A_Mushroom("Lavaball", 8);
		INFR O 5 Bright;
		INFR P 5;
		INFR Q 5;
		Stop;
	}
}

Class FireTrailparticle : Actor
{
  Default
  {
    radius 6;
    height 8;
    speed 1;
    damage 0;
    renderstyle "Add";
    +NOBLOCKMAP
    +NOGRAVITY
    +MISSILE
    +NOTELEPORT
    +DONTSPLASH
  }

  states
  {
  Spawn:
    PCMT DEFGHI 1 bright;
    stop;
  }
}